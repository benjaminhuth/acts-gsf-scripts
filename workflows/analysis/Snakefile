configfile: "config/config.yaml"

envvars:
    "ODD_DIR",

SEEDING_DICT = {
    "1GeV_to_100GeV": "truth_estimated",
    "muons": "truth_estimated",
    "4GeV": "smeared",
}

rule simulate_electrons:
    output:
        "tmp/1GeV_to_100GeV/particles.root",
        "tmp/1GeV_to_100GeV/hits.root",
    params:
        n_events=1000,
        p_min=1,
        p_max=100,
        p_transverse=True,
        uniform_eta=True,
        pdg=11,
    script:
        "scripts/simulation.py"
        
    
rule simulate_electrons_4GeV:
    output:
        "tmp/4GeV/hits.root",
        "tmp/4GeV/particles.root",
    params:
        n_events=20,
        p_min=4,
        p_max=4,
        p_transverse=False,
        uniform_eta=False,
        pdg=11,
    script:
        "scripts/simulation.py"


rule simulate_muons:
    output:
        "tmp/muons/particles.root",
        "tmp/muons/hits.root",
    params:
        n_events=1000,
        p_min=1,
        p_max=100,
        p_transverse=True,
        uniform_eta=True,
        pdg=13,
    script:
        "scripts/simulation.py"


rule run_gsf_12cmps:
    output:
        "tmp/{dataset}/gsf12/tracksummary_gsf.root",
        "tmp/{dataset}/gsf12/trackstates_gsf.root",
    input:
        "tmp/{dataset}/particles.root",
        "tmp/{dataset}/hits.root",
    params:
        components=12,
        weight_cutoff=0.0001,
        seeding=lambda wildcards: SEEDING_DICT[wildcards.dataset],
    script:
        "scripts/run_gsf.py"


rule run_gsf_1cmp:
    output:
        "tmp/{dataset}/gsf1/tracksummary_gsf.root",
        "tmp/{dataset}/gsf1/trackstates_gsf.root",
    input:
        "tmp/{dataset}/particles.root",
        "tmp/{dataset}/hits.root",
    params:
        components=1,
        weight_cutoff=0.0001,
        seeding=lambda wildcards: SEEDING_DICT[wildcards.dataset],
    script:
        "scripts/run_gsf.py"


rule run_kf:
    output:
        "tmp/{dataset}/kf/tracksummary_kf.root",
        "tmp/{dataset}/kf/trackstates_kf.root",
    input:
        "tmp/{dataset}/particles.root",
        "tmp/{dataset}/hits.root",
    params:
        seeding=lambda wildcards: SEEDING_DICT[wildcards.dataset],
    script:
        "scripts/run_kf.py"

rule all_residual_plot:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
    output:
        "plots/all_residuals.pdf"
    script:
        "scripts/plot_all_residuals.py"

rule detailed_perf_plots:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
        "tmp/1GeV_to_100GeV/gsf1/tracksummary_gsf.root",
    output:
        "plots/gsf_vs_kf_res.pdf",
        "plots/gsf_vs_gsf_res.pdf",
        "plots/pulls.pdf",
    script:
        "scripts/plot_detailed_perf.py"


rule histograms_eta_pt:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/gsf1/tracksummary_gsf.root",
    output:
        "plots/res_vs_eta_pt.pdf"
    script:
        "scripts/plot_residuals_2d_hist.py"


rule sweep_components_weight_cutoff:
    input:
        "tmp/1GeV_to_100GeV/particles.root",
        "tmp/1GeV_to_100GeV/hits.root",
    output:
        "tmp/sweep_results.csv"
    params:
        n_events=10,
        apply_selection=True,
        core_quantile=0.95,
    script:
        "scripts/parameters_sweep.py"


rule kf_muon_vs_gsf_electron:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
        "tmp/muons/kf/tracksummary_kf.root",
    output:
        "plots/kf_muon_vs_gsf_electron.pdf",
    params:
        log=False,
    script:
        "scripts/plot_resolution_muon_vs_electron.py"


rule plot_sweep:
    input:
        "tmp/sweep_results.csv",
    output:
        "plots/sweep_components.pdf",
        "plots/sweep_weight_cutoff.pdf",
        "plots/sweep_reduction_method.pdf"
    script:
        "scripts/plot_sweep.py"

rule plot_eloss_correlation:
    input:
        "tmp/4GeV/gsf12/tracksummary_gsf.root",
        "tmp/4GeV/gsf12/trackstates_gsf.root",
        "tmp/4GeV/kf/tracksummary_kf.root",
        "tmp/4GeV/kf/trackstates_kf.root",
    output:
        "plots/correlation_res_total_eloss.pdf",
        "plots/correlation_res_eloss_first_surface.pdf",
    script:
        "scripts/plot_eloss_correlation.py"

rule plot_outliers_holes:
    input:
        "tmp/1GeV_to_100GeV/particles.root",
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
    output:
        "plots/outliers_holes.pdf",
    script:
        "scripts/plot_outliers_holes.py"

rule all:
    default_target: True
    input:
        "plots/all_residuals.pdf",
        "plots/gsf_vs_gsf_res.pdf",
        "plots/gsf_vs_kf_res.pdf",
        "plots/pulls.pdf",
        "plots/res_vs_eta_pt.pdf",
        "plots/sweep_components.pdf",
        "plots/sweep_weight_cutoff.pdf",
        "plots/correlation_res_total_eloss.pdf",
        "plots/correlation_res_eloss_first_surface.pdf",
        "plots/kf_muon_vs_gsf_electron.pdf",
        "plots/outliers_holes.pdf",
