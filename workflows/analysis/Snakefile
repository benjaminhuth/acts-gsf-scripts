from pathlib import Path

configfile: "config/config.yaml"

envvars:
    "ODD_DIR",

SEEDING_DICT = {
    "1GeV_to_100GeV": "truth_estimated",
    "muons": "truth_estimated",
    "10GeV": "smeared",
}

rule simulate_electrons:
    output:
        "tmp/1GeV_to_100GeV/particles.root",
        "tmp/1GeV_to_100GeV/hits.root",
    params:
        n_events=1000,
        p_min=1,
        p_max=100,
        abs_eta=3,
        p_transverse=True,
        uniform_eta=True,
        pdg=11,
    script:
        "scripts/simulation.py"
        
    
rule simulate_electrons_10GeV:
    output:
        "tmp/10GeV/hits.root",
        "tmp/10GeV/particles.root",
    params:
        n_events=200,
        p_min=10,
        p_max=10,
        abs_eta=3,
        p_transverse=False,
        uniform_eta=True,
        pdg=11,
    script:
        "scripts/simulation.py"


rule simulate_muons:
    output:
        "tmp/muons/particles.root",
        "tmp/muons/hits.root",
    params:
        n_events=1000,
        p_min=1,
        p_max=100,
        abs_eta=3,
        p_transverse=True,
        uniform_eta=True,
        pdg=13,
    script:
        "scripts/simulation.py"


rule run_gsf_12cmps:
    output:
        "tmp/{dataset}/gsf12/tracksummary_gsf.root",
        "tmp/{dataset}/gsf12/trackstates_gsf.root",
    input:
        "tmp/{dataset}/particles.root",
        "tmp/{dataset}/hits.root",
    params:
        components=12,
        weight_cutoff=1e-6,
        seeding=lambda wildcards: SEEDING_DICT[wildcards.dataset],
    script:
        "scripts/run_gsf.py"


rule run_gsf_1cmp:
    output:
        "tmp/{dataset}/gsf1/tracksummary_gsf.root",
        "tmp/{dataset}/gsf1/trackstates_gsf.root",
    input:
        "tmp/{dataset}/particles.root",
        "tmp/{dataset}/hits.root",
    params:
        components=1,
        weight_cutoff=1e-6,
        seeding=lambda wildcards: SEEDING_DICT[wildcards.dataset],
    script:
        "scripts/run_gsf.py"


rule run_kf:
    output:
        "tmp/{dataset}/kf/tracksummary_kf.root",
        "tmp/{dataset}/kf/trackstates_kf.root",
        "tmp/{dataset}/kf/timing.tsv",
    input:
        "tmp/{dataset}/particles.root",
        "tmp/{dataset}/hits.root",
    params:
        seeding=lambda wildcards: SEEDING_DICT[wildcards.dataset],
    script:
        "scripts/run_kf.py"

rule all_residual_plot:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
    output:
        "plots/all_residuals.pdf"
    script:
        "scripts/plot_all_residuals.py"

rule detailed_perf_plots:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
        "tmp/1GeV_to_100GeV/gsf1/tracksummary_gsf.root",
    output:
        "plots/gsf_vs_kf_res.pdf",
        "plots/gsf_vs_gsf_res.pdf",
        "plots/pulls.pdf",
    script:
        "scripts/plot_detailed_perf.py"


rule histograms_eta_pt:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/gsf1/tracksummary_gsf.root",
    output:
        "plots/res_vs_eta_pt.pdf"
    script:
        "scripts/plot_residuals_2d_hist.py"


rule sweep_components_weight_cutoff:
    input:
        "tmp/1GeV_to_100GeV/particles.root",
        "tmp/1GeV_to_100GeV/hits.root",
    output:
        "tmp/sweep_results.csv"
    params:
        n_events=10,
        apply_selection=True,
        core_quantile=0.95,
    script:
        "scripts/parameters_sweep.py"


rule kf_muon_vs_gsf_electron:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
        "tmp/muons/kf/tracksummary_kf.root",
    output:
        "plots/kf_muon_vs_gsf_electron.pdf",
    params:
        log=False,
    script:
        "scripts/plot_resolution_muon_vs_electron.py"


rule pickle_summary:
    input:
        "tmp/{dataset}/{fitter1}/tracksummary_{fitter2}.root",
        "tmp/{dataset}/{fitter1}/trackstates_{fitter2}.root"
    output:
        "tmp/{dataset}/{fitter1}/tracksummary_{fitter2}.pickle"
    script:
        "scripts/pickle.py"
    
        
rule plot_sweep:
    input:
        "tmp/sweep_results.csv",
    output:
        "plots/sweep_components.pdf",
        "plots/sweep_weight_cutoff.pdf",
        "plots/sweep_component_merge_method.pdf",
        "plots/sweep_reduction_algorithm.pdf",
        "plots/sweep_bh_approx.pdf",
        "plots/sweep_overview.pdf",
    params:
        fix_components=12,
        fix_weight_cutoff=1.e-6,
    script:
        "scripts/plot_sweep.py"

rule plot_eloss_correlation:
    input:
        "tmp/10GeV/gsf12/tracksummary_gsf.pickle",
        "tmp/10GeV/kf/tracksummary_kf.pickle",
    output:
        "plots/correlation_res_total_eloss.pdf",
        "plots/correlation_res_eloss_first_surface.pdf",
    script:
        "scripts/plot_eloss_correlation.py"

rule plot_outliers_holes:
    input:
        "tmp/1GeV_to_100GeV/particles.root",
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
    output:
        "plots/outliers_holes.pdf",
    script:
        "scripts/plot_outliers_holes.py"

rule plot_cummulative_res:
    input:
        "tmp/1GeV_to_100GeV/gsf12/tracksummary_gsf.root",
        "tmp/1GeV_to_100GeV/kf/tracksummary_kf.root",
    output:
        "plots/cummulative_res.pdf"
    script:
        "scripts/plot_cummulative_residual.py"
        
rule run_gsf_12_10GeV_different_bha:
    output:
        "tmp/10GeV/gsf12_simple_bha/tracksummary_gsf.root",
        "tmp/10GeV/gsf12_simple_bha/trackstates_gsf.root",
    input:
        "tmp/10GeV/particles.root",
        "tmp/10GeV/hits.root",
    params:
        components=12,
        weight_cutoff=1e-6,
        seeding="smeared",
        use_non_geant_bha=True
    script:
        "scripts/run_gsf.py"


rule momentum_last_surface:
    input:
        "tmp/10GeV/{gsf}/tracksummary_gsf.pickle",
        "tmp/10GeV/kf/tracksummary_kf.pickle",
    output:
        "plots/momentum_at_last_surface_{gsf}.pdf",
        "plots/momentum_at_last_surface_center_{gsf}.pdf",
        "plots/momentum_at_last_surface_bad_samples_{gsf}.pdf",
    script:
        "scripts/plot_momentum_last_surface.py"


rule all:
    default_target: True
    input:
        "plots/all_residuals.pdf",
        "plots/gsf_vs_gsf_res.pdf",
        "plots/gsf_vs_kf_res.pdf",
        "plots/pulls.pdf",
        "plots/res_vs_eta_pt.pdf",
        "plots/sweep_components.pdf",
        "plots/sweep_weight_cutoff.pdf",
        "plots/correlation_res_total_eloss.pdf",
        "plots/correlation_res_eloss_first_surface.pdf",
        "plots/kf_muon_vs_gsf_electron.pdf",
        "plots/outliers_holes.pdf",
        "plots/cummulative_res.pdf",
        "plots/momentum_at_last_surface_gsf12.pdf",
        "plots/momentum_at_last_surface_gsf12_simple_bha.pdf",
        "plots/sweep_bh_approx.pdf",
